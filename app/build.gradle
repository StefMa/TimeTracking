apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

def crashlyticsEnabled = false

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "guru.stefma.timetracking"
        minSdkVersion 19
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"

        buildConfigField "boolean", "CRASHLYTICS_ENABLED", "$crashlyticsEnabled"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
        dev {
            def useIncreasedMinSdk = properties.get("increaseMinSdk", true)
            if (useIncreasedMinSdk) {
                minSdkVersion 21
            }

            /**
             * Crashlytics Beta Configuration
             *
             * More information at https://docs.fabric.io/android/beta/gradle.html
             */
            ext.enableCrashlytics = crashlyticsEnabled
            ext.betaDistributionEmails = ""
            ext.betaDistributionNotifications = true
            ext.betaDistributionReleaseNotes = ""
        }
        prod {
            ext.enableCrashlytics = crashlyticsEnabled
        }
    }
    dataBinding {
        enabled true
    }
}

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

dependencies {
    devCompile project(path: ':restapi', configuration: 'devRelease')
    prodCompile project(path: ':restapi', configuration: 'prodRelease')

    def androidSupportVersion = '23.4.0'
    compile "com.android.support:appcompat-v7:$androidSupportVersion"
    compile "com.android.support:design:$androidSupportVersion"
    compile "com.android.support:cardview-v7:$androidSupportVersion"
    compile "com.android.support:support-annotations:$androidSupportVersion"
    compile "com.android.support:preference-v7:$androidSupportVersion"
    compile 'com.prolificinteractive:material-calendarview:1.2.0'
    compile('com.crashlytics.sdk.android:crashlytics:2.5.7@aar') {
        transitive = true;
    }

    testCompile 'junit:junit:4.12'
}
